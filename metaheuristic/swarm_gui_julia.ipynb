{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Particle Swarm Optimization Visualizer\n",
    "---\n",
    "Kyle Spurlock\n",
    "\n",
    "12/11/2023"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Functions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "function rastrigin(x)\n",
    "    20 + sum(x .^ 2 + 10 * cos.(2π * x))\n",
    "end\n",
    "\n",
    "function schaffer_n4(x)\n",
    "    x1 = x[1]\n",
    "    x2 = x[2]\n",
    "    a = cos(sin(abs(x1^2 - x2^2)))^2 - 0.5\n",
    "    b = (1 + 0.001*(x1^2 + x2^2))^2\n",
    "    c = 0.5 + a + b\n",
    "end\n",
    "\n",
    "function levy(x)\n",
    "    w(u) = 1 + (u - 1) / 4\n",
    "    ws = w.(x)\n",
    "\n",
    "    function inner_levy(z)\n",
    "        a = sin(π*z[1])^2\n",
    "        b = ((z[1]-1)^2)*(1 + 10sin(π*z[1] + 1)^2)\n",
    "        c = ((z[2]-1)^2)*(1 + sin(2π*z[2])^2)     \n",
    "        a + b + c\n",
    "    end\n",
    "    inner_levy(ws)\n",
    "end\n",
    "\n",
    "\n",
    "function surface_grid(func, domain)\n",
    "    n = length(domain)\n",
    "    xx, yy, zz = zeros(n, n), zeros(n, n), zeros(n, n)\n",
    "\n",
    "    for (i, x) in pairs(domain)\n",
    "        for (j, y) in pairs(domain)\n",
    "            xx[i, j] = x\n",
    "            yy[i, j] = y\n",
    "            zz[i, j] = func([x, y]) \n",
    "        end\n",
    "\n",
    "    end \n",
    "    vec(xx), vec(yy), vec(zz)\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Particle Swarm Optimization (PSO) Algorithm"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "\n",
    "struct SwarmState\n",
    "    positions\n",
    "    velocities\n",
    "    costs\n",
    "\n",
    "    best_costs\n",
    "    best_pos\n",
    "\n",
    "    best_global_cost\n",
    "    best_global_pos\n",
    "end\n",
    "\n",
    "function update_best(costs, positions, best_costs, best_pos, best_global_cost, best_global_pos)\n",
    "    new_best_costs, best_inds = findmin(hcat(costs, best_costs), dims=2)\n",
    "\n",
    "    new_best_pos = copy(best_pos)\n",
    "    temp = [positions, best_pos]\n",
    "\n",
    "    for (i, ind) in enumerate(best_inds)\n",
    "        # Update best positions \n",
    "        # Second index is important here as this specifies which is better of new/old \n",
    "        new_best_pos[i, :] = temp[ind[2]][i, :]\n",
    "    end\n",
    "\n",
    "    # Update best overall\n",
    "    local_min_cost, local_min_ind = findmin(new_best_costs)\n",
    "\n",
    "    new_best_global_cost = best_global_cost\n",
    "    new_best_global_pos = copy(best_global_pos)\n",
    "\n",
    "    if local_min_cost < best_global_cost\n",
    "        new_best_global_cost = local_min_cost\n",
    "        new_best_global_pos = new_best_pos[local_min_ind[1], :]\n",
    "    end\n",
    "\n",
    "    new_best_costs, new_best_pos, new_best_global_cost, new_best_global_pos\n",
    "end\n",
    "\n",
    "function update_velocity(velocities, positions, best_pos, best_global_pos, rng; constrict=0.01, inertia=1, damping=[-1, 1])\n",
    "    # Dimension-wise random values for stochasticity\n",
    "    phi1 = rand(Float32, size(positions))\n",
    "    phi2 = rand(Float32, size(positions))\n",
    "\n",
    "    b = phi1 .* best_pos .- positions\n",
    "    c = phi2 .* best_global_pos' .- positions\n",
    "    new_velocities = constrict * (inertia * velocities + b + c)\n",
    "\n",
    "    # Damp the velocities from becoming too large\n",
    "    new_velocities[findall(<(damping[1]), new_velocities)] .= damping[1]\n",
    "    new_velocities[findall(>(damping[2]), new_velocities)] .= damping[2]\n",
    "\n",
    "    new_velocities\n",
    "end\n",
    "\n",
    "\n",
    "function update_position(positions, velocities; bounds=nothing)\n",
    "    new_pos = positions + velocities\n",
    "\n",
    "    # Ensure position is bounded\n",
    "    if !isnothing(bounds)\n",
    "        new_pos[findall(<(bounds[1]), new_pos)] .= bounds[1]\n",
    "        new_pos[findall(>(bounds[2]), new_pos)] .= bounds[2]\n",
    "    end\n",
    "    new_pos\n",
    "end\n",
    "\n",
    "function swarm_optimize(num_p, criterion, domain=-5.12:0.1:5.12; iterations=50, constrict=0.01, inertia=1, damping=[-1, 1], seed=nothing)\n",
    "    bounds = [minimum(domain), maximum(domain)]\n",
    "    rng = Random.seed!(seed)\n",
    "\n",
    "    # Initialize swarm ---------------------------------\n",
    "    positions = rand(rng, domain, num_p, 2)\n",
    "    velocities = zeros(num_p, 2)\n",
    "\n",
    "    costs = criterion.(eachrow(positions))\n",
    "\n",
    "    best_costs = copy(costs)\n",
    "    best_pos = copy(positions)\n",
    "\n",
    "    best_global_cost, best_global_ind = findmin(costs)\n",
    "    best_global_pos = positions[best_global_ind, :]\n",
    "    \n",
    "    velocities = update_velocity(velocities, positions, best_pos, best_global_pos, rng; constrict=constrict, inertia=inertia, damping=damping )\n",
    "\n",
    "    initial_state = SwarmState(positions, velocities, costs, best_costs, best_pos, best_global_cost, best_global_pos)\n",
    "    states = [initial_state]\n",
    "    \n",
    "    # Loop --------------------------------------------\n",
    "    for i in range(1, iterations-1)\n",
    "        positions = update_position(states[i].positions, states[i].velocities; bounds=bounds)\n",
    "        costs = criterion.(eachrow(positions))\n",
    "\n",
    "        best_costs, best_pos, best_global_cost, best_global_pos = update_best(costs, positions, states[i].best_costs, states[i].best_pos, states[i].best_global_cost, states[i].best_global_pos)\n",
    "        velocities = update_velocity(states[i].velocities, positions, best_pos, best_global_pos, rng; constrict=constrict, inertia=inertia, damping=damping)\n",
    "\n",
    "        new_state = SwarmState(positions, velocities, costs, best_costs, best_pos, best_global_cost, best_global_pos)\n",
    "        push!(states, new_state)\n",
    "    end\n",
    "    states\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Visualizer Code"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie\n",
    "GLMakie.activate!(title=\"Particle Swarm Visualizer\")\n",
    "using Statistics, LinearAlgebra\n",
    "\n",
    "\n",
    "# Figure components ----------------------------------------------\n",
    "fig = Figure(resolution=(1600, 800), backgroundcolor=:lightgrey)\n",
    "\n",
    "grid_options = fig[1, 1] = GridLayout()\n",
    "grid_main = fig[1, 2] = GridLayout()\n",
    "\n",
    "ax3 = Axis3(grid_main[2, 1])\n",
    "ax21 = Axis(grid_main[2, 2], xrectzoom=false, yrectzoom=false, yzoomlock=true, xzoomlock=true)\n",
    "ax22 = Axis(grid_main[3, 1:2], xlabel=\"Time\", ylabel=\"Cost\")\n",
    "\n",
    "\n",
    "# Menu -----------------------------------------------------------\n",
    "funcs = [rastrigin, schaffer_n4, levy]\n",
    "func = Observable{Any}(funcs[1])\n",
    "\n",
    "menu = Menu(fig,\n",
    "    options = zip([\"Rastrigin\", \"Schaffer N4\", \"Levy\"], funcs),\n",
    "    default = \"Rastrigin\")\n",
    "\n",
    "on(menu.selection) do s\n",
    "    func[] = s\n",
    "end\n",
    "notify(menu.selection)\n",
    "\n",
    "\n",
    "# System parameters ---------------------------------------------------------------\n",
    "low_bound = Observable{Float32}(-5.12)\n",
    "step_size = Observable{Float32}(0.01)\n",
    "high_bound = Observable{Float32}(5.12)\n",
    "domain = Observable{Vector{Float32}}(collect(low_bound[]:step_size[]:high_bound[]))\n",
    "\n",
    "num_particles = Observable{Int32}(5)\n",
    "iterations = Observable{Int32}(50)\n",
    "constrict = Observable{Float32}(0.01)\n",
    "inertia = Observable{Float32}(1)\n",
    "damp_low = Observable{Float32}(-1)\n",
    "damp_high = Observable{Float32}(1)\n",
    "\n",
    "seed = Observable{Any}(nothing)\n",
    "\n",
    "initial_states = swarm_optimize(\n",
    "        num_particles[],\n",
    "        func[],\n",
    "        domain[];\n",
    "        iterations=iterations[],\n",
    "        constrict=constrict[],\n",
    "        inertia=inertia[],\n",
    "        damping=[damp_low[], damp_high[]]\n",
    "        )\n",
    "\n",
    "states = Observable(initial_states)\n",
    "\n",
    "px, py, pz = [Observable{Vector{Float64}}() for _ in range(1, 3)] # Positions\n",
    "vu, vv = [Observable{Vector{Float64}}() for _ in range(1, 2)] # Velocities\n",
    "\n",
    "# These are the stored statistics to avoid recomputing when changing slider\n",
    "stats_x = Observable([z for z in range(1, 50)]) # Used to represent timesteps when plotting statistics\n",
    "mins = Observable([minimum(s.costs) for s in initial_states])\n",
    "means = Observable([mean(s.costs) for s in initial_states])\n",
    "maxs = Observable([maximum(s.costs) for s in initial_states])\n",
    "\n",
    "# These are the statistics points plotted up to time t\n",
    "data_min = Observable{Matrix{Float64}}()\n",
    "data_mean = Observable{Matrix{Float64}}()\n",
    "data_max = Observable{Matrix{Float64}}()\n",
    "\n",
    "# Slider -------------------------------------------------------------------------\n",
    "sg = SliderGrid(fig, (label=\"Time\", range = 1:1:50, startvalue=1))\n",
    "\n",
    "lift(sg.sliders[1].value) do i\n",
    "    # Update positions for plotting\n",
    "    pos = states[][i].positions\n",
    "    npx = pos[:, 1]\n",
    "    npy = pos[:, 2]\n",
    "    npz = states[][i].costs\n",
    "    \n",
    "    px.val = npx\n",
    "    py.val = npy\n",
    "    pz.val = npz\n",
    "\n",
    "    # Update velocities for plotting\n",
    "    vel = states[][i].velocities\n",
    "    norms = norm.(eachrow(vel))\n",
    "    normed_vel = vel ./ norms\n",
    "    nvu = normed_vel[:, 1]\n",
    "    nvv = normed_vel[:, 2]\n",
    "\n",
    "    vu.val = nvu\n",
    "    vv[] = nvv # Delay update\n",
    "    notify(pz) # Notify pz to also notify px and py\n",
    "    \n",
    "    # update statistics for plotting\n",
    "    selected_x = stats_x[][1:i]\n",
    "    data_min[] = hcat(selected_x, mins[][1:i])\n",
    "    data_mean[] = hcat(selected_x, means[][1:i])\n",
    "    data_max[] = hcat(selected_x, maxs[][1:i])\n",
    "end\n",
    "\n",
    "# Buttons ------------------------------------------------------------------------\n",
    "b_opt = Button(fig, label=\"Optimize\", tellwidth=:false)\n",
    "on(b_opt.clicks) do n\n",
    "    # Get new swarm states\n",
    "    new_states = swarm_optimize(\n",
    "        num_particles[],\n",
    "        func[],\n",
    "        domain[];\n",
    "        iterations=iterations[],\n",
    "        constrict=constrict[],\n",
    "        inertia=inertia[],\n",
    "        damping=[damp_low[], damp_high[]],\n",
    "        seed=seed[]\n",
    "        )\n",
    "\n",
    "    states[] = new_states\n",
    "    sg.sliders[1].range = 1:iterations[] # Change slider range\n",
    "\n",
    "    # Compute new plotted elements related to statistics\n",
    "    stats_x[] = [z for z in range(1, iterations[])]\n",
    "    mins[] = [minimum(s.costs) for s in new_states]\n",
    "    means[] = [mean(s.costs) for s in new_states]\n",
    "    maxs[] = [maximum(s.costs) for s in new_states]\n",
    "    \n",
    "    # Update axes of statistics plot to reflect new statistics\n",
    "    xlims!(ax22, 1, iterations[] + 0.5)\n",
    "    ylims!(ax22, minimum(mins[])-0.5, maximum(maxs[])+0.5)\n",
    "\n",
    "    set_close_to!(sg.sliders[1], 1) # Reset the slider (will also reset point positions)\n",
    "end\n",
    "\n",
    "b_plot = Button(fig, label=\"Adjust plot\", tellwidth=:false)\n",
    "on(b_plot.clicks; priority=-1) do n\n",
    "    domain[] = collect(low_bound[]:step_size[]:high_bound[])\n",
    "    \n",
    "    # Change the plotted data\n",
    "    nxx, nyy, nzz = surface_grid(func[], domain[])\n",
    "    xx.val = nxx\n",
    "    yy.val = nyy\n",
    "    zz[] = nzz # Delay update\n",
    "\n",
    "    # Surface plot\n",
    "    xlims!(ax3, minimum(xx[]), maximum(xx[]))\n",
    "    ylims!(ax3, minimum(yy[]), maximum(yy[]))\n",
    "    zlims!(ax3, minimum(zz[]), maximum(zz[]))\n",
    "    \n",
    "    # Contour plot\n",
    "    xlims!(ax21, minimum(xx[]), maximum(xx[]))\n",
    "    ylims!(ax21, minimum(yy[]), maximum(yy[]))\n",
    "    notify(b_opt.clicks)\n",
    "end\n",
    "\n",
    "xx = Observable{Vector{Float64}}()\n",
    "yy = Observable{Vector{Float64}}()\n",
    "zz = Observable{Vector{Float64}}()\n",
    "notify(b_plot.clicks) # Will initialize xx, yy, zz\n",
    "\n",
    "surf = surface!(ax3, xx, yy, zz, colormap=:viridis)\n",
    "co = contour!(ax21, xx, yy, zz, colormap=:viridis)\n",
    "Colorbar(grid_main[1, 1:2], surf, vertical=false)\n",
    "\n",
    "ylims!(ax22, minimum(mins[]), maximum(maxs[]))\n",
    "xlims!(ax22, minimum(stats_x[]), maximum(stats_x[]))\n",
    "\n",
    "# Textboxes ----------------------------------------------------------------------------------------------\n",
    "\n",
    "# Plot bounds\n",
    "t_low_bound = Textbox(fig, placeholder=\"-5.12\", tellwidth=:false, validator=Float32, boxcolor=:white)\n",
    "t_step = Textbox(fig, placeholder=\"0.01\", tellwidth=:false, validator=Float32, boxcolor=:white)\n",
    "t_high_bound = Textbox(fig, placeholder=\"5.12\", tellwidth=:false, validator=Float32, boxcolor=:white)\n",
    "\n",
    "# Swarm parameters\n",
    "t_num_particles = Textbox(fig, placeholder=\"5\", tellwidth=:false,width=100, validator=Int32, boxcolor=:white, halign=:right)\n",
    "t_iterations = Textbox(fig, placeholder=\"50\", tellwidth=:false,width=100, validator=Int32, boxcolor=:white, halign=:right)\n",
    "t_constrict = Textbox(fig, placeholder=\"0.01\", tellwidth=:false,width=100, validator=Float32, boxcolor=:white, halign=:right)\n",
    "t_inertia = Textbox(fig, placeholder=\"1\", tellwidth=:false,width=100, validator=Float32, boxcolor=:white, halign=:right)\n",
    "t_low_damp = Textbox(fig, placeholder=\"-1\", tellwidth=:false, validator=Float32, boxcolor=:white)\n",
    "t_high_damp = Textbox(fig, placeholder=\"1\", tellwidth=:false, validator=Float32, boxcolor=:white)\n",
    "t_seed = Textbox(fig, placeholder=\"-1\", tellwidth=:false, width=100,validator=Int32, boxcolor=:white, halign=:right)\n",
    "\n",
    "# Textbox actions\n",
    "on(t_low_bound.stored_string) do s\n",
    "    low_bound[] = parse(Float32, s)\n",
    "end\n",
    "\n",
    "on(t_step.stored_string) do s\n",
    "    step_size[] = parse(Float32, s)\n",
    "end\n",
    "\n",
    "on(t_high_bound.stored_string) do s\n",
    "    high_bound[] = parse(Float32, s)\n",
    "end\n",
    "\n",
    "on(t_num_particles.stored_string) do s\n",
    "    num_particles[] = parse(Int32, s)\n",
    "end\n",
    "\n",
    "on(t_iterations.stored_string) do s\n",
    "    iterations[] = parse(Int32, s)\n",
    "end\n",
    "\n",
    "on(t_constrict.stored_string) do s\n",
    "    constrict[] = parse(Float32, s)\n",
    "end\n",
    "\n",
    "on(t_inertia.stored_string) do s\n",
    "    inertia[] = parse(Float32, s)\n",
    "end\n",
    "\n",
    "on(t_low_damp.stored_string) do s\n",
    "    damp_low[] = parse(Float32, s)\n",
    "end\n",
    "\n",
    "on(t_high_damp.stored_string) do s\n",
    "    damp_high[] = parse(Float32, s)\n",
    "end\n",
    "\n",
    "on(t_seed.stored_string) do s\n",
    "    new_seed = parse(Int64, s)\n",
    "    if new_seed < 0\n",
    "        seed[] = nothing\n",
    "    else\n",
    "        seed[] = new_seed\n",
    "    end\n",
    "end\n",
    "\n",
    "# Arranging option objects ------------------------------------------\n",
    "plot_bounds_grid = hgrid!(Label(fig, \"LB\"), t_low_bound, Label(fig, \"Step\"), t_step, Label(fig, \"UB\"), t_high_bound)\n",
    "damp_grid = hgrid!(Label(fig, \"Low Damp\"), t_low_damp, Label(fig, \"High Damp\"), t_high_damp)\n",
    "\n",
    "grid_options[1:3, 1, ] = vgrid!(\n",
    "    Label(fig, \"Function\", tellwidth=:false, halign=:left),\n",
    "    menu,\n",
    "    plot_bounds_grid,\n",
    "    b_plot,\n",
    "    hgrid!(Label(fig, \"Particles\", halign=:right), t_num_particles),\n",
    "    hgrid!(Label(fig, \"Iterations\", halign=:right), t_iterations),\n",
    "    hgrid!(Label(fig, \"Constrict\", halign=:right), t_constrict),\n",
    "    hgrid!(Label(fig, \"Inertia\", halign=:right),t_inertia),\n",
    "    damp_grid,\n",
    "    hgrid!(Label(fig, \"Seed\", halign=:right), t_seed),\n",
    "    sg,\n",
    "    b_opt\n",
    "    )\n",
    "\n",
    "# Final plotting -----------------------------------------------------\n",
    "scatter!(ax3, px, py, pz; color=:red)\n",
    "scatter!(ax21, px, py, pz; color=:red)\n",
    "arrows!(ax21, px, py, vu, vv; lengthscale=0.5)\n",
    "max_line = lines!(ax22, data_max, label=\"max\")\n",
    "mean_line = lines!(ax22, data_mean, label=\"mean\")\n",
    "min_line = lines!(ax22, data_min, label=\"min\")\n",
    "\n",
    "axislegend(ax22, position=:lb)\n",
    "\n",
    "colsize!(fig.layout, 2, Relative(7/10))\n",
    "colgap!(fig.layout, 100)\n",
    "\n",
    "fig"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
